import com.eriwen.gradle.css.tasks.MinifyCssTask
import com.eriwen.gradle.js.tasks.MinifyJsTask

buildscript {
    ext {
        springBootVersion = '2.0.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "com.eriwen.gradle.css" version "2.14.0"
    id "com.eriwen.gradle.js" version "2.14.1"
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'css'
apply plugin: 'js'

version = '2.0.0'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

css.source {
    custom {
        css {
            srcDir "src/main/resources/static/css"
            include "**/*.css"
            exclude "**/*.min.css"
        }
    }
}

javascript.source {
    custom {
        js {
            srcDir "src/main/resources/static/js"
            include "**/*.js"
            exclude "**/*.min.js"
        }
    }
}

css.source.custom.css.files.eachWithIndex { cssFile, idx ->
    tasks.create(name: "minifyCss_${cssFile.getName().replaceFirst(/[.][^.]+$/, "")}", type: MinifyCssTask) {
        source = cssFile
        dest = cssFile.getAbsolutePath().replace('.css', '.min.css')

        yuicompressor {
            lineBreakPos = -1
        }
    }
}

javascript.source.custom.js.files.eachWithIndex { jsFile, idx ->
    tasks.create(name: "minifyJs_${jsFile.getName().replaceFirst(/[.][^.]+$/, "")}", type: MinifyJsTask) {
        def absolutePath = jsFile.getAbsolutePath()

        source = jsFile
        dest = absolutePath.replace('.js', '.min.js')
        sourceMap = absolutePath.replace('.js', '.min.js.map')

        closure {
            warningLevel = 'QUIET'
        }
        doLast {
            def sourceMapFile = file(absolutePath.replace('.js', '.min.js.map'))
            sourceMapFile.write(sourceMapFile.text.replaceAll("\\[\".*src/main/resources/static", "[\""))

            file(absolutePath.replace('.js', '.min.js')) << "\n//# sourceMappingURL=" + jsFile.getName().replace('.js', '.min.js.map')
        }
    }
}

task minifyAllCss(dependsOn: tasks.matching { Task task ->
    task.name.startsWith("minifyCss_")
})

task minifyAllJs(dependsOn: tasks.matching { Task task ->
    task.name.startsWith("minifyJs_")
})

task minify(dependsOn: [minifyAllCss, minifyAllJs])

bootJar.dependsOn minify


repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-cache'
    compile group: 'org.springframework.session', name: 'spring-session-jdbc'

    compile group: 'com.googlecode.htmlcompressor', name: 'htmlcompressor', version: '1.5.2'
    compile group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.9'
    compile group: 'de.svenkubiak', name: 'jBCrypt', version: '0.4.1'
    compile group: 'com.google.guava', name: 'guava', version: '24.1-jre'
    compile group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
    compile group: 'com.yahoo.platform.yui', name: 'yuicompressor', version: '2.4.8'

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'

    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.6'

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
}
